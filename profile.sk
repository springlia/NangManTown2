command /프로필 [<offlineplayer>]:
	trigger:
		if arg-1 is not set:
			set {_targetP} to player
		else:
			set {_targetP} to arg-1
		if {profile::%{_targetP}%::design} is not set:
			set {profile::%{_targetP}%::design} to 0
		set {_profileGUI} to chest inventory with 6 rows named ":offset_-8::profile_%{profile::%{_targetP}%::design}%:"
		
		#내용 셋팅
		set slot 19 of {_profileGUI} to skull of {_targetP} named "&e&l%{_targetP}%&f님의 프로필"
		set slot 10 of {_profileGUI} to {voidItem} named "&e한마디"
		
		set slot 3 of {_profileGUI} to {%{profile::%{_targetP}%::magic}%_badge} #기숙사
		set slot 5 of {_profileGUI} to {%{profile::%{_targetP}%::score}%_badge} #성적
		set slot 7 of {_profileGUI} to {%{profile::%{_targetP}%::grade}%grade_badge} #학년

		#전투력
		set {_val} to {profile::%{_targetP}%::power}
		if {_val} is between 0 and 100:
			set slot 21 of {_profileGUI} to {bronze_badge} 
		else if {_val} is between 101 and 200:
			set slot 21 of {_profileGUI} to {silver_badge}
		else if {_val} is between 201 and 300:
			set slot 21 of {_profileGUI} to {gold_badge}
		else if {_val} is between 301 and 400:
			set slot 21 of {_profileGUI} to {dia_badge}

		set {_badge} to findBadge({_targetP}, "level")
		set slot 23 of {_profileGUI} to {_badge} #레벨

		#파티
		set {_party} to {nowParty::%{_targetP}%}
		set slot 25 of {_profileGUI} to {%{_party}%::m1}'s skull 

		set {_badge} to findBadge({_targetP}, "gathering")
		set slot 46 of {_profileGUI} to {_badge} #채집

		set {_badge} to findBadge({_targetP}, "cooking")
		set slot 48 of {_profileGUI} to {_badge} #요리

		set {_badge} to findBadge({_targetP}, "fishing")
		set slot 50 of {_profileGUI} to {_badge} #낚시

		set {_badge} to findBadge({_targetP}, "alchemy")
		set slot 52 of {_profileGUI} to {_badge} #연금

		open {_profileGUI} to player

command /프로필장식:
	permission: op
	trigger:
		set {_editProfileGUI} to chest inventory with 1 rows named "프로필 디자인 선택"
		set slot 0 of {_editProfileGUI} to white wool named "&f일반 프로필" with lore "&7클릭하여 선택"
		set slot 2 of {_editProfileGUI} to black wool named "&7블랙 프로필" with lore "&7클릭하여 선택"
		set slot 4 of {_editProfileGUI} to lime wool named "&a겜타벅스 프로필" with lore "&7클릭하여 선택"
		set slot 6 of {_editProfileGUI} to orange wool named "&e골드 프로필" with lore "&7클릭하여 선택"
		set slot 8 of {_editProfileGUI} to pink wool named "&d벚꽃 프로필" with lore "&7클릭하여 선택"
		open {_editProfileGUI} to player

on inventory click:
	event-inventory's name is "프로필 디자인 선택":
		cancel event
		clicked slot is 0:
			send "&e[프로필] &f프로필 디자인 설정 완료! &7[ 현재 디자인 : 일반 프로필 ]" to player
			set {profile::%player%::design} to 0
		clicked slot is 2:
			send "&e[프로필] &f프로필 디자인 설정 완료! &7[ 현재 디자인 : 블랙 프로필 ]" to player
			set {profile::%player%::design} to 1
		clicked slot is 4:
			send "&e[프로필] &f프로필 디자인 설정 완료! &7[ 현재 디자인 : 겜타벅스 프로필 ]" to player
			set {profile::%player%::design} to 2
		clicked slot is 6:
			send "&e[프로필] &f프로필 디자인 설정 완료! &7[ 현재 디자인 : 골드 프로필 ]" to player
			set {profile::%player%::design} to 3
		clicked slot is 8:
			send "&e[프로필] &f프로필 디자인 설정 완료! &7[ 현재 디자인 : 벚꽃 프로필 ]" to player
			set {profile::%player%::design} to 4
	event-inventory's name contains ":offset_-8::profile_":
		cancel event


function findBadge(targetP: offlineplayer, key: string) :: item:
	set {_val} to {profile::%{_targetP}%::%{_key}%}
	if {_val} is between 0 and 14:
		return {bronze_badge}
	else if {_val} is between 15 and 39:
		return {silver_badge}
	else if {_val} is between 40 and 59:
		return {gold_badge}
	else if {_val} is between 60 and 75:
		return {dia_badge}

on right click on player:
	if player is sneaking:
		if clicked entity is not player:
			execute player command "/정보 %clicked entity%"

