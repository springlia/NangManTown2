
command /요리:
	permission: op
	trigger:
		set {_cookGUI} to chest inventory with 6 rows named ":offset_-8::recipe:"	

		##요리 레시피##
		set slot 10 of {_cookGUI} to {salad} named "&a&l샐러드" with lore "&7=== 필요 재료 ===" and "&f- 토마토 &ex1" and "&f- 양배추 &ex1"

		##다른 요리들##

		open {_cookGUI} to player

on inventory click:
	if name of event-inventory is ":offset_-8::recipe:":

		set {_cookingGUI} to chest inventory with 6 rows named ":offset_-8::cook:"
		set slot 39 of {_cookingGUI} to {voidItem} named "&a요리 시작"
		set slot 41 of {_cookingGUI} to {voidItem} named "&c요리 취소"

		set {_i} to 28
		while {_i} < 35:
			set slot {_i} of {_cookingGUI} to gray stained glass pane named "&f"
			add 1 to {_i}	

		cancel event

		clicked slot is 10:
			set slot 13 of {_cookingGUI} to {salad} named "&a&l샐러드"
			if check [player has 1 of {tomato}]->[player has 1 of {cabage}]:
				set {nowCook::%player%} to {salad}
				open {_cookingGUI} to player
			else:
				send "&c재료가 부족합니다." to player
				play sound "block.note_block.bass" with pitch 0.5 to player


		##다른 음식들##

	if name of event-inventory is ":offset_-8::cook:":
		cancel event

		clicked slot is 39:
			if {isNowCooking::%player%} is not set:
				if check [player has 1 of {tomato}]->[player has 1 of {cabage}]:
					set {isNowCooking::%player%} to true
					remove 1 of {tomato} from player
					remove 1 of {cabage} from player
					add {tomato} to {useCook::%player%::*}
					add {cabage} to {useCook::%player%::*}
					Cooking(player)
				else:
					send "&c재료가 부족합니다." to player
					play sound "block.note_block.bass" with pitch 0.5 to player
			else:
				send "&c이미 요리 중입니다." to player

		clicked slot is 41:
			delete {isNowCooking::%player%}
			send "&c요리가 취소되었습니다." to player
			close player's inventory
			set {stopCook::%player%} to true
			

on inventory close:
	if name of event-inventory is ":offset_-8::cook:":
		if check [{isNowCooking::%player%} is set]->[{stopCook::%player%} is not set]:
			wait 1 tick
			open event-inventory to player
			send "&c요리 진행 중에는 인벤토리를 닫을 수 없습니다." to player


	if name of event-inventory is ":offset_-8::finish_cook:":
		set {_backItem} to item in slot 4 of event-inventory
		if {_backItem} is not air:
			if player's inventory can hold {_backItem}:
				give {_backItem} to player
			else:
				send "&c인벤토리에 공간이 없어 아이템을 받을 수 없었습니다. 관리자에게 문의하세요." to player
				send "&7[관리모드] &6&l[요리] &e%player% &f사라진 아이템 : %{_backItem}%" to console


function Cooking(player: player):
	send "&a요리 진행 중..." to {_player}

	set {_i} to 28
	while {_i} < 35:
		wait 1 seconds
		set slot {_i} of {_player}'s current inventory to lime stained glass pane named "&f"
		

		if {stopCook::%{_player}%} is true:
			loop {useCook::%{_player}%::*}:
				give loop-value to {_player}
			delete {nowCook::%{_player}%}
			delete {useCook::%{_player}%::*}
			delete {stopCook::%{_player}%}
			wait 1 tick
			stop

		play sound "block.brewing_stand.brew" with pitch 1 to {_player}
		add 1 to {_i}

	delete {useCook::%{_player}%::*}
	delete {isNowCooking::%{_player}%}
	
	set {_giveCook} to chest inventory with 1 rows named ":offset_-8::finish_cook:"
	play sound "entity.experience_orb.pickup" with pitch 1.2 to {_player}
	set slot 4 of {_giveCook} to {nowCook::%{_player}%}
	open {_giveCook} to {_player}

	send "&7[관리모드] %{_player}%님이 요리를 완성했습니다. 완성된 요리: %{nowCook::%{_player}%}%" to console
	delete {nowCook::%{_player}%}
	
	
